{-# STDLIB_VERSION 5 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

let USDN = base58'DG2xFkPdDwKUoBkzGAhQtLpSGzfXLiCYPEzeKH2Ad24p'
let issuerNftDapp = base58'3P8LVqVKhTViMJau96KNir6FhHr4WnfoW57'

let royaltyPercent = 10

func checkNFT(asset: Asset) = {
  asset.issuer.bytes == issuerNftDapp && asset.decimals == 0 && asset.reissuable == false && asset.quantity == 1
}

func tryGetInteger(key: String) = {
  match getInteger(this, key) {
      case b:Int => b
      case _ => 0
  }
}

func tryGetString(key: String) = {
  match getString(this, key) {
      case b:String => b
      case _ => ""
  }
}

func tryGetAssetInfo(assetId: String, errorMsg: String) = {
  match assetInfo(assetId.fromBase58String()) {
    case a:Asset => a
    case _ => throw(errorMsg)
  }
}

func getAssetString(assetId: ByteVector|Unit) = {
  match assetId {
    case b:ByteVector => b.toBase58String()
    case _ => "WAVES"
  }
}

func getAssetBytes(assetIdStr: String) = {
  if (assetIdStr == "WAVES") then {unit} else {assetIdStr.fromBase58String()}
}

@Callable(i)
func sellNFT(buyOutPrice: Int, buyOutAssetId: String) = {

  let pmt = if i.payments.size() == 1 && i.payments[0].amount == 1
    then i.payments[0]
    else throw("NFT not listed as payment")

  let buyOutAsset = buyOutAssetId.getAssetBytes()

  if (isDefined(pmt.assetId)) then {
    let nftId = value(pmt.assetId)
    let nft = match assetInfo(nftId) {
      case a:Asset => a
      case _ => throw("Can't find NFT")
    }

    if (checkNFT(nft)) then {
      if (buyOutPrice > 0) then {

        let assetNameStr = toBase58String(nft.id)
        let royaltyReceiver = match getString(Address(issuerNftDapp), "nft_" + assetNameStr + "_issuer") {
          case a: String => a
          case _ => i.caller.toString()
        }
        
        [
          IntegerEntry("nft_" + assetNameStr + "_buyOutPrice", buyOutPrice),
          StringEntry("nft_" + assetNameStr + "_buyOutAssetId", buyOutAsset.getAssetString()),
          StringEntry("nft_" + assetNameStr + "_royaltyReceiver", royaltyReceiver),
          StringEntry("nft_" + assetNameStr + "_owner", i.caller.toString()),
          IntegerEntry("address_" + i.caller.toString() + "_nft_" + assetNameStr + "_startSaleAt", height)
        ]

      } else throw("Price must be greater than zero")
    } else throw("NFT issued by another dapp")
  } else throw("NFT is not attached to the transaction")
}

@Callable(i)
func buyNFT(nftId: String) = {
  let pmt = if i.payments.size() == 1
    then i.payments[0]
    else throw("Attached payment is required")

  let buyOutPrice = tryGetInteger("nft_" + nftId + "_buyOutPrice")
  let buyOutAssetId = tryGetString("nft_" + nftId + "_buyOutAssetId")
  let royaltyReceiver = tryGetString("nft_" + nftId + "_royaltyReceiver")
  let ownerStr = tryGetString("nft_" + nftId + "_owner")

  let nftAsset = tryGetAssetInfo(nftId, "NFT not found")
  let buyOutAsset = tryGetAssetInfo(buyOutAssetId, "Buyout asset not found")

  if (buyOutPrice > 0 && pmt.amount >= buyOutPrice) then {

    # calculate royalty
    let royaltyPart = fraction(buyOutPrice, royaltyPercent, 100)

    if (pmt.assetId == buyOutAsset.id) then {
      [
        ScriptTransfer(i.caller, 1, nftAsset.id),
        ScriptTransfer(Address(ownerStr.fromBase58String()), buyOutPrice - royaltyPart, buyOutAsset.id),
        ScriptTransfer(Address(royaltyReceiver.fromBase58String()), royaltyPart, buyOutAsset.id),

        DeleteEntry("nft_" + nftId + "_buyOutPrice"),
        DeleteEntry("nft_" + nftId + "_buyOutAssetId"),
        DeleteEntry("nft_" + nftId + "_royaltyReceiver"),
        DeleteEntry("nft_" + nftId + "_owner"),
        DeleteEntry("address_" + ownerStr + "_nft_" + nftId + "_startSaleAt")
      ]
    } else throw("The buyout asset does not match the purchase asset")
  } else throw("Payment amount must be greater than zero")
}

@Callable(i)
func cancelSell(nftId: String) = {
  let ownerStr = tryGetString("nft_" + nftId + "_owner")

  let nftAsset = tryGetAssetInfo(nftId, "NFT not found")

  if (size(ownerStr) > 0 && ownerStr == i.caller.toString()) then {
    [
      ScriptTransfer(i.caller, 1, nftAsset.id),

      DeleteEntry("nft_" + nftId + "_buyOutPrice"),
      DeleteEntry("nft_" + nftId + "_buyOutAssetId"),
      DeleteEntry("nft_" + nftId + "_royaltyReceiver"),
      DeleteEntry("nft_" + nftId + "_owner"),
      DeleteEntry("address_" + ownerStr + "_nft_" + nftId + "_startSaleAt")
    ]
  } else throw("You are not owner")
}

@Verifier(tx)
func verify() = sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)